buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.flipkart.krystal:vajram:' + project.krystal_version
    }
}

plugins {
    id 'java-gradle-plugin'
    id 'groovy'
}

group 'com.flipkart.krystal'
version project.krystal_version
description
'''Gradle plugin for projects using the Krystal framework. 
Includes Vajram code generator and validator. 
Tasks from this plugin are added under the group "Krystal"'''

gradlePlugin {
    plugins {
        simplePlugin {
            id = 'com.flipkart.krystal'
            implementationClass = 'com.flipkart.krystal.vajram.VajramPlugin'
        }
    }
}

tasks.named('spotlessJava').configure { mustRunAfter('compileGroovy') }

dependencies {
    api project(':vajram')
    implementation 'com.squareup:javapoet:1.13.0'
    implementation 'commons-cli:commons-cli:1.5.0'
    implementation 'org.projectlombok:lombok:1.18.24'

    compileOnly 'com.google.auto.service:auto-service:1.1.1'
    annotationProcessor 'com.google.auto.service:auto-service:1.1.1'

    def jacksonVersion = '2.14.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:' + jacksonVersion
    implementation 'com.fasterxml.jackson.core:jackson-databind:' + jacksonVersion
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:' + jacksonVersion
}

checkerFramework {
    extraJavacArgs = [
            "-Astubs=${rootDir}/config/checker/stubs",
            '-AresolveReflection',
    ]
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.flipkart.krystal'
            artifactId = 'krystal-plugin'
            version = project.version
            from components.java
            pom {
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}