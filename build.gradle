buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:6.16.0'
    }
}

ext {
    clojarsusername = project.properties['clojarsusername'] ?: ""
    clojarspassword = project.properties['clojarspassword'] ?: ""
    krystal_version = '3.3-SNAPSHOT'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'com.diffplug.spotless'

    group 'com.flipkart.krystal'
    version project.krystal_version

    spotless {
        java {
            target 'src/*/java/**/*.java', 'build/generated/sources/vajrams/**/*.java'
            googleJavaFormat()
        }
    }

    dependencies {
        implementation 'org.checkerframework:checker-qual:3.21.3'
        implementation 'org.slf4j:slf4j-api:2.0.3'

        compileOnly 'org.projectlombok:lombok:1.18.24'
        annotationProcessor 'org.projectlombok:lombok:1.18.24'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
        testImplementation 'org.assertj:assertj-core:3.23.1'

        testCompileOnly 'org.projectlombok:lombok:1.18.24'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'


    }

    afterEvaluate {
        tasks.named('spotlessCheck') {
            dependsOn('spotlessApply')
        }
    }

    java {
        withSourcesJar()
    }

    publishing {
        repositories {
            maven {
                url "https://clojars.org/repo"
                credentials {
                    username = rootProject.ext.clojarsusername
                    password = rootProject.ext.clojarspassword
                }
            }
        }
    }

    test {
        useJUnitPlatform()
    }
}